apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7
targetCompatibility = 1.7

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

dependencies {
    compile project(':fastdex-common')
    compile 'org.apache.ant:ant:1.8.2'
    compile 'com.google.guava:guava:18.0'
    compile 'com.android.tools.ddms:ddmlib:25.3.1'

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.code.gson:gson:2.3.1'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }

        resources {
            srcDir 'src/main/resources'
        }
    }
}

apply from: rootProject.file('bintray.gradle')


def generated = new File("${project.buildDir}/generated/java")
sourceSets {
    main {
        java {
            srcDir generated
        }
    }
}

task generateVersionConstantsJava {
    inputs.property("version", version)
    ext.versionFile = new File(generated, "com/github/typ0520/fastdex/Version.java")
    outputs.file(versionFile)
}

generateVersionConstantsJava << {
    versionFile.parentFile.mkdirs()
    versionFile.text = """
package com.github.typ0520.fastdex;

public final class Version {
    private Version() {}
    public static final String FASTDEX_BUILD_VERSION = "$version";
}
"""
}

tasks.compileJava.dependsOn generateVersionConstantsJava