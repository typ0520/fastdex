apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def projectName = project.name
def mavenDesc = projectName
def baseUrl = 'https://github.com/typ0520/fastdex'
def siteUrl = baseUrl
def gitUrl = "${baseUrl}.git"
def issueUrl = "${baseUrl}/issues"

def licenseIds = ['Apache-2.0']
def licenseNames = ['The Apache Software License, Version 2.0']
def licenseUrls = ['http://www.apache.org/licenses/LICENSE-2.0.txt']
def inception = '2017'

Properties props = new Properties()
try {
    props.load(project.rootProject.file('local.properties').newDataInputStream())
} catch (Throwable e) {
    e.printStackTrace()
}

def bintray_user = props.getProperty("BINTRAY_USER","")
def bintray_key = props.getProperty("BINTRAY_KEY","")

install {
    repositories {
        mavenInstaller {
            pom.project {
                // Description
                name projectName
                description mavenDesc
                url siteUrl

                // Archive
                groupId project.group
                artifactId archivesBaseName
                version project.version

                // License
                inceptionYear inception
                licenses {
                    licenseNames.eachWithIndex { ln, li ->
                        license {
                            name ln
                            url licenseUrls[li]
                        }
                    }
                }
                developers {
                    developer {
                        name bintray_user
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}


bintray {
    user = bintray_user
    key = bintray_key
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = "${project.group}:${projectName}"
        desc = mavenDesc
        websiteUrl = siteUrl
        issueTrackerUrl = issueUrl
        vcsUrl = gitUrl
        labels = ['android', 'plugin', 'gradle', 'fastdex']
        licenses = licenseIds
        publish = true
        publicDownloadNumbers = true
    }
}